# Is Flash installed and enabled?
# http://stackoverflow.com/a/20095467/1575238
hasFlash = ->
  try
    flash = new ActiveXObject('ShockwaveFlash.ShockwaveFlash')
    return !!flash
  catch e
    return navigator.mimeTypes and
           navigator.mimeTypes['application/x-shockwave-flash'] isnt undefined and
           navigator.mimeTypes['application/x-shockwave-flash'].enabledPlugin

ZeroClipboardLink = ($scope, element, attrs) ->
  return element.hide() unless hasFlash()

  ZeroClipboard.config swfPath: '<%= asset_path('zero-clipboard.swf') %>'

  client = new ZeroClipboard(element)
  messageLifetime = 2500

  onAfterCopy = ->
    element.addClass('zero-show-message')
    element.blur()

    onTimeout = -> element.removeClass('zero-show-message')
    setTimeout onTimeout, messageLifetime

  client.on 'ready', -> client.on 'aftercopy', onAfterCopy

  # For some reason, ZeroClipboard is not the best at removing this class when
  # the mouse is moving quickly...
  element.mouseleave -> element.removeClass('zeroclipboard-is-hover')

zeroClipboard = ->
  restrict: 'A'
  replace: true
  templateUrl: 'angular/templates/zero_clipboard.html'
  link: ZeroClipboardLink

angular.module('pandify').directive('zeroClipboard', zeroClipboard)
